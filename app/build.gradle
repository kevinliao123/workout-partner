apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "com.fruitguy.workoutpartner"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 2
        versionName rootProject.ext.versionName
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file("${System.env.JKS_LOCATION}")
            storePassword "${System.env.KEYSTORE_PASSWORD}"
            keyAlias "${System.env.KEY_ALIAS}"
            keyPassword "${System.env.KEY_PASSWORD}"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            minifyEnabled false
            debuggable true
            applicationIdSuffix ".debug"
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = outputFileName.replace("app-", "${rootProject.ext.appName}-${variant.versionName}-")
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.facebook.android:facebook-login:$rootProject.ext.facebookVersion"
    implementation("com.twitter.sdk.android:twitter-core:$rootProject.ext.twitterVersion@aar") {
        transitive = true
    }
    implementation "com.google.android.gms:play-services-nearby:$rootProject.ext.firebaseVersion"
    implementation "com.google.android.gms:play-services-auth:$rootProject.ext.firebaseVersion"
    implementation "com.google.firebase:firebase-auth:$rootProject.ext.firebaseVersion"
    implementation "com.google.android.gms:play-services-gcm:$rootProject.ext.firebaseVersion"
    implementation "com.google.firebase:firebase-core:$rootProject.ext.firebaseVersion"
    implementation "com.google.firebase:firebase-database:$rootProject.ext.firebaseVersion"
    implementation "com.google.firebase:firebase-storage:$rootProject.ext.firebaseVersion"
    implementation "com.google.firebase:firebase-messaging:$rootProject.ext.firebaseVersion"
    implementation "com.firebaseui:firebase-ui-auth:$rootProject.ext.firebaseUiVersion"
    implementation "com.firebaseui:firebase-ui-database:$rootProject.ext.firebaseUiVersion"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.1'

    implementation "com.android.support:support-v4:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    implementation "com.android.support:animated-vector-drawable:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:customtabs:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.ext.supportLibraryVersion"
    implementation "com.android.support:support-vector-drawable:$rootProject.ext.supportLibraryVersion"

    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation "com.android.volley:volley:$rootProject.ext.vollyVersion"
    implementation "com.google.code.gson:gson:$rootProject.ext.gsonVersion"
    implementation "com.squareup.picasso:picasso:$rootProject.ext.picassoVersion"
    implementation "com.squareup.okhttp:okhttp:$rootProject.ext.okhttpVersion"
    implementation "com.google.guava:guava:$rootProject.guavaVersion"
    implementation "de.hdodenhof:circleimageview:2.2.0"
    implementation "com.github.yalantis:ucrop:2.2.1"
    implementation 'com.android.support:multidex:1.0.3'
    debugImplementation "com.facebook.stetho:stetho:$rootProject.ext.stethoVersion"
    implementation "com.jakewharton:butterknife:$rootProject.ext.butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterknifeVersion"
    implementation 'id.zelory:compressor:2.1.0'

    // Dagger dependencies
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
    implementation "com.google.dagger:dagger:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$rootProject.daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"

    testImplementation "junit:junit:$rootProject.ext.junitVersion"
    testImplementation "org.mockito:mockito-core:$rootProject.ext.mockitoVersion"
    androidTestImplementation "com.android.support.test:runner:$rootProject.ext.runnerVersion"
    androidTestImplementation "com.android.support.test:rules:$rootProject.ext.rulesVersion"
    androidTestImplementation "org.hamcrest:hamcrest-library:$rootProject.ext.hamcrestVersion"
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
}

apply plugin: 'com.google.gms.google-services'
